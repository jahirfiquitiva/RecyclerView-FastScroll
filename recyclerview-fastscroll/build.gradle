apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'dev.jahir'
version = '2.0.3'

android {
    compileSdkVersion 35
    buildToolsVersion "35.0.0"
    namespace "com.simplecityapps.recyclerview_fastscroll"

    defaultConfig {
        minSdkVersion 14
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))

    //RecyclerView
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    // AppCompat
    implementation 'androidx.appcompat:appcompat:1.7.0'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.findByName('release')
                groupId = 'dev.jahir'
                artifactId = 'recyclerview-fastscroll'
                version = '2.0.3'
                artifact bundleReleaseAar
            }
        }
    }
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
