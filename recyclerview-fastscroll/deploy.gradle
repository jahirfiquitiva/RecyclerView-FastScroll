/*
 * Copyright (c) 2016 Tim Malseed
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'maven-publish'

def Properties bintrayProps = new Properties()
File bintrayProperties = file('bintray.properties')
if (bintrayProperties.exists()) {
    bintrayProps.load(new FileInputStream(bintrayProperties))
}

group = bintrayProps['group']
version = bintrayProps['version']

def pkgName = bintrayProps['pkgName']

def publishJavadoc = bintrayProps['publishJavadoc']
def publishSources = bintrayProps['publishSources']

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

artifacts {
    if (publishJavadoc) {
        archives javadocJar
    }
    if (publishSources) {
        archives sourcesJar
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = bintrayProps['group']
                artifactId = pkgName
                version = bintrayProps['version']
            }
        }
    }
}
